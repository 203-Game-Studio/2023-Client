#pragma kernel ComputeGaussianRandom
#pragma kernel CreateHeightSpectrum
#pragma kernel CreateDisplaceSpectrum
#pragma kernel FFTHorizontal
#pragma kernel FFTHorizontalEnd
#pragma kernel FFTVertical
#pragma kernel FFTVerticalEnd
#pragma kernel TextureGenerationDisplace
#pragma kernel TextureGenerationNormalBubbles

#define PI 3.14159274f
#define G 9.81f

int _N;					//fft纹理大小
float _OceanLength;		//海洋长度
float _A;				//phillips谱参数，影响波浪高度
float4 _WindAndSeed;		//风和随机种子 xy为风, zw为两个随机种子
float _Time;				//时间
float _Lambda;			//偏移影响
float _HeightScale;		//高度影响
float _BubblesScale;	    //泡沫强度
float _BubblesThreshold; //泡沫阈值
int _Ns;					//Ns = pow(2,m-1); m为第几阶段
uint rngState;           //随机

RWTexture2D<float4> _GaussianRandomRT;		//高斯随机数
RWTexture2D<float4> _HeightSpectrumRT;		//高度频谱
RWTexture2D<float4> _DisplaceXSpectrumRT;	//X偏移频谱
RWTexture2D<float4> _DisplaceZSpectrumRT;	//Z偏移频谱
RWTexture2D<float4> _DisplaceRT;				//最后生成的偏移纹理
RWTexture2D<float4> _InputRT;				//输入
RWTexture2D<float4> _OutputRT;				//输出
RWTexture2D<float4> _NormalRT;				//法线纹理
RWTexture2D<float4> _BubblesRT;				//泡沫纹理

//随机种子
uint wangHash(uint seed)
{
    seed = (seed ^ 61) ^(seed >> 16);
    seed *= 9;
    seed = seed ^(seed >> 4);
    seed *= 0x27d4eb2d;
    seed = seed ^(seed >> 15);
    return seed;
}

float rand()
{
    // Xorshift算法
    rngState ^= (rngState << 13);
    rngState ^= (rngState >> 17);
    rngState ^= (rngState << 5);
    return rngState / 4294967296.0f;;
}

//计算高斯随机数
float2 gaussian(float2 id)
{
    //均匀分布随机数
    rngState = wangHash(id.y * _N + id.x);
    float x1 = rand();
    float x2 = rand();

    x1 = max(1e-6f, x1);
    x2 = max(1e-6f, x2);
    //计算两个相互独立的高斯随机数
    float g1 = sqrt(-2.0f * log(x1)) * cos(2.0f * PI * x2);
    float g2 = sqrt(-2.0f * log(x1)) * sin(2.0f * PI * x2);

    return float2(g1, g2);
}
[numthreads(8, 8, 1)]
void ComputeGaussianRandom(uint3 id: SV_DispatchThreadID)
{
    float2 g = gaussian(id.xy);

    _GaussianRandomRT[id.xy] = float4(g, 0, 0);
}

//phillips谱
float phillips(float2 k)
{
    float kLength = length(k);
    kLength = max(0.001f, kLength);
    // kLength = 1;
    float kLength2 = kLength * kLength;
    float kLength4 = kLength2 * kLength2;

    float windLength = length(_WindAndSeed.xy);
    float  l = windLength * windLength / G;
    float l2 = l * l;

    float damping = 0.001f;
    float L2 = l2 * damping * damping;

    //phillips谱
    return _A * exp(-1.0f / (kLength2 * l2)) / kLength4 * exp(-kLength2 * L2);
}

//弥散
float dispersion(float2 k)
{
    return sqrt(G * length(k));
}

//Donelan-Banner方向拓展
float DonelanBannerDirectionalSpreading(float2 k)
{
    float betaS;
    float omegap = 0.855f * G / length(_WindAndSeed.xy);
    float ratio = dispersion(k) / omegap;

    if (ratio < 0.95f)
    {
        betaS = 2.61f * pow(ratio, 1.3f);
    }
    if(ratio >= 0.95f && ratio < 1.6f)
    {
        betaS = 2.28f * pow(ratio, -1.3f);
    }
    if(ratio > 1.6f)
    {
        float epsilon = -0.4f + 0.8393f * exp(-0.567f * log(ratio * ratio));
        betaS = pow(10, epsilon);
    }
    float theta = atan2(k.y, k.x) - atan2(_WindAndSeed.y, _WindAndSeed.x);

    return betaS / max(1e-7f, 2.0f * tanh(betaS * PI) * pow(cosh(betaS * theta), 2));
}

//复数相乘
float2 complexMultiply(float2 c1, float2 c2)
{
    return float2(c1.x * c2.x - c1.y * c2.y,
    c1.x * c2.y + c1.y * c2.x);
}

//高度频谱
[numthreads(8, 8, 1)]
void CreateHeightSpectrum(uint3 id: SV_DispatchThreadID)
{
    float2 k = float2(2.0f * PI * id.x / _N - PI, 2.0f * PI * id.y / _N - PI);

    float2 gaussian = _GaussianRandomRT[id.xy].xy;

    float2 hTilde0 = gaussian * sqrt(abs(phillips(k) * DonelanBannerDirectionalSpreading(k)) / 2.0f);
    float2 hTilde0Conj = gaussian * sqrt(abs(phillips(-k) * DonelanBannerDirectionalSpreading(-k)) / 2.0f);
    hTilde0Conj.y *= -1.0f;

    float omegat = dispersion(k) * _Time;
    float c = cos(omegat);
    float s = sin(omegat);
    
    float2 h1 = complexMultiply(hTilde0, float2(c, s));
    float2 h2 = complexMultiply(hTilde0Conj, float2(c, -s));

    float2 hTilde = h1 + h2;

    _HeightSpectrumRT[id.xy] = float4(hTilde, 0, 0);
}

//偏移频谱
[numthreads(8, 8, 1)]
void CreateDisplaceSpectrum(uint3 id: SV_DispatchThreadID)
{
    float2 k = float2(2 * PI * id.x / _N - PI, 2 * PI * id.y / _N - PI);
    k /= max(0.001f, length(k));
    float2 HTilde = _HeightSpectrumRT[id.xy].xy;

    float2 KxHTilde = complexMultiply(float2(0, -k.x), HTilde);
    float2 kzHTilde = complexMultiply(float2(0, -k.y), HTilde);

    _DisplaceXSpectrumRT[id.xy] = float4(KxHTilde, 0, 0);
    _DisplaceZSpectrumRT[id.xy] = float4(kzHTilde, 0, 0);
}

//横向FFT
[numthreads(8, 8, 1)]
void FFTHorizontal(uint3 id: SV_DispatchThreadID)
{
    int2 idxs = id.xy;
    idxs.x = floor(id.x / (_Ns * 2.0f)) * _Ns + id.x % _Ns;
    float angle = 2.0f * PI * (id.x / (_Ns * 2.0f));
    float2 w = float2(cos(angle), sin(angle));

    float2 x0 = _InputRT[idxs].xy;
    float2 x1 = _InputRT[int2(idxs.x + _N * 0.5f, idxs.y)].xy;

    float2 output = x0 + float2(w.x * x1.x - w.y * x1.y, w.x * x1.y + w.y * x1.x);
    _OutputRT[id.xy] = float4(output, 0, 0);
}
[numthreads(8, 8, 1)]
void FFTHorizontalEnd(uint3 id: SV_DispatchThreadID)
{
    int2 idxs = id.xy;
    idxs.x = floor(id.x / (_Ns * 2.0f)) * _Ns + id.x % _Ns;
    float angle = 2.0f * PI * (id.x / (_Ns * 2.0f));
    float2 w = float2(cos(angle), sin(angle));

    /*********修改内容***********/
    w *= -1;
    /***************************/

    float2 x0 = _InputRT[idxs].xy;
    float2 x1 = _InputRT[int2(idxs.x + _N * 0.5f, idxs.y)].xy;

    float2 output = x0 + float2(w.x * x1.x - w.y * x1.y, w.x * x1.y + w.y * x1.x);
    /*********修改内容***********/
    int x = id.x - _N * 0.5f;
    output *= ((x + 1) % 2.0f) * 1 + (x % 2.0f) * (-1);
    /***************************/
    _OutputRT[id.xy] = float4(output, 0, 0);
}

//纵向FFT
[numthreads(8, 8, 1)]
void FFTVertical(uint3 id: SV_DispatchThreadID)
{
    int2 idxs = id.xy;
    idxs.y = floor(id.y / (_Ns * 2.0f)) * _Ns + id.y % _Ns;
    float angle = 2.0f * PI * (id.y / (_Ns * 2.0f));
    float2 w = float2(cos(angle), sin(angle));

    float2 x0 = _InputRT[idxs].xy;
    float2 x1 = _InputRT[int2(idxs.x, idxs.y + _N * 0.5f)].xy;

    float2 output = x0 + float2(w.x * x1.x - w.y * x1.y, w.x * x1.y + w.y * x1.x);
    _OutputRT[id.xy] = float4(output, 0, 0);
}
[numthreads(8, 8, 1)]
void FFTVerticalEnd(uint3 id: SV_DispatchThreadID)
{
    int2 idxs = id.xy;
    idxs.y = floor(id.y / (_Ns * 2.0f)) * _Ns + id.y % _Ns;
    float angle = 2.0f * PI * (id.y / (_Ns * 2.0f));
    float2 w = float2(cos(angle), sin(angle));

    /*********修改内容***********/
    w *= -1;
    /***************************/

    float2 x0 = _InputRT[idxs].xy;
    float2 x1 = _InputRT[int2(idxs.x, idxs.y + _N * 0.5f)].xy;

    float2 output = x0 + float2(w.x * x1.x - w.y * x1.y, w.x * x1.y + w.y * x1.x);
    /*********修改内容***********/
    int x = id.y - _N * 0.5f;
    output *= ((x + 1) % 2.0f) * 1 + (x % 2.0f) * (-1);
    /***************************/
    _OutputRT[id.xy] = float4(output, 0, 0);
}

//偏移
[numthreads(8, 8, 1)]
void TextureGenerationDisplace(uint3 id: SV_DispatchThreadID)
{
    float y = length(_HeightSpectrumRT[id.xy].xy) / (_N * _N) * _HeightScale;//高度
    float x = length(_DisplaceXSpectrumRT[id.xy].xy) / (_N * _N) * _Lambda;//x轴偏移
    float z = length(_DisplaceZSpectrumRT[id.xy].xy) / (_N * _N) * _Lambda;//z轴偏移
    
    _HeightSpectrumRT[id.xy] = float4(y, y, y, 0);
    _DisplaceXSpectrumRT[id.xy] = float4(x, x, x, 0);
    _DisplaceZSpectrumRT[id.xy] = float4(z, z, z, 0);
    _DisplaceRT[id.xy] = float4(x, y, z, 0);
}

//法线和泡沫
[numthreads(8, 8, 1)]
void TextureGenerationNormalBubbles(uint3 id: SV_DispatchThreadID)
{
    //计算法线
    float uintLength = _OceanLength / (_N - 1.0f);//两点间单位长度
    //获取当前点，周围4个点的uv坐标
    uint2 uvX1 = uint2((id.x - 1.0f + _N) % _N, id.y);
    uint2 uvX2 = uint2((id.x + 1.0f + _N) % _N, id.y);
    uint2 uvZ1 = uint2(id.x, (id.y - 1.0f + _N) % _N);
    uint2 uvZ2 = uint2(id.x, (id.y + 1.0f + _N) % _N);

    //以当前点为中心，获取周围4个点的偏移值
    float3 x1D = _DisplaceRT[uvX1].xyz;//在x轴 第一个点的偏移值
    float3 x2D = _DisplaceRT[uvX2].xyz;//在x轴 第二个点的偏移值
    float3 z1D = _DisplaceRT[uvZ1].xyz;//在z轴 第一个点的偏移值
    float3 z2D = _DisplaceRT[uvZ2].xyz;//在z轴 第二个点的偏移值

    //以当前点为原点，构建周围4个点的坐标
    float3 x1 = float3(x1D.x - uintLength, x1D.yz);//在x轴 第一个点的坐标
    float3 x2 = float3(x2D.x + uintLength, x2D.yz);//在x轴 第二个点的坐标
    float3 z1 = float3(z1D.xy, z1D.z - uintLength);//在z轴 第一个点的坐标
    float3 z2 = float3(z1D.xy, z1D.z + uintLength);//在z轴 第二个点的坐标

    //计算两个切向量
    float3 tangentX = x2 - x1;
    float3 tangentZ = z2 - z1;

    //法线
    float3 normal = normalize(cross(tangentZ, tangentX));

    //泡沫
    float3 ddx = x2D - x1D;
    float3 ddz = z2D - z1D;
    //雅可比行列式
    float jacobian = (1.0f + ddx.x) * (1.0f + ddz.z) - ddx.z * ddz.x;

    jacobian = saturate(max(0, _BubblesThreshold - saturate(jacobian)) * _BubblesScale);

    _NormalRT[id.xy] = float4(normal, 0);
    _BubblesRT[id.xy] = float4(jacobian, jacobian, jacobian, 0);
}