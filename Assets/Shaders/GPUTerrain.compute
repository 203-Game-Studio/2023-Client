#pragma kernel TraverseQuadTree
#pragma kernel BuildPatches

#define PATCH_COUNT_PER_NODE 8

struct NodeDescriptor{
    uint branch;
};

struct RenderPatch{
    float2 position;
    float2 minMaxHeight;
    uint lod;
    //uint4 lodTrans;
};

struct Bound{
    float3 min;
    float3 max;
};

Texture2D<float4> _MinMaxHeightTexture;

ConsumeStructuredBuffer<uint2> _NodeListA;
AppendStructuredBuffer<uint2> _NodeListB;
AppendStructuredBuffer<uint3> _FinalNodeList;
RWStructuredBuffer<NodeDescriptor> _NodeDescriptors;
AppendStructuredBuffer<RenderPatch> _CulledPatchList;
StructuredBuffer<uint3> _RenderNodeList;

uniform uint _LodLevel;
uniform float3 _CameraPosWS;
uniform float3 _WorldSize;
uniform int _BoundsHeightRedundance;

//x为nodeSize(m)
//y等于nodeSize/16
//z等于worldSize/nodeSize
//w等于2^lodLevel
uniform float4 _NodeParams[6];

uniform uint _NodeIDOffsets[6];

uniform float4x4 _VPMatrix;

float3 GetNodePositionWS(uint2 node){
    float nodeMeterSize = _NodeParams[_LodLevel].x;
    float nodeCount = _NodeParams[_LodLevel].z;
    float2 nodePosWS = ((float2)node - (nodeCount - 1) * 0.5) * nodeMeterSize;
    float2 minMaxHeight = _MinMaxHeightTexture.mips[_LodLevel][node].xy;
    float y = (minMaxHeight.x + minMaxHeight.y) * 0.5 * _WorldSize.y;
    return float3(nodePosWS.x, y, nodePosWS.y);
}

bool EvaluateNode(uint2 node){
    float3 nodePosWS = GetNodePositionWS(node);
    float3 dv = _CameraPosWS - nodePosWS;
    float distance2 = dv.x*dv.x + dv.y*dv.y + dv.z*dv.z;
    float nodeSize = _NodeParams[_LodLevel].x;
    float f2 = distance2 / (nodeSize * nodeSize * 1.44);
    if(f2 < 1){
        return true;
    }
    return false;
}

uint GetNodeID(uint2 node){
    return _NodeIDOffsets[_LodLevel] + node.y * _NodeParams[_LodLevel].z + node.x;
}

[numthreads(1, 1, 1)]
void TraverseQuadTree(uint3 id : SV_DispatchThreadID)
{
    uint2 node = _NodeListA.Consume();
    uint nodeID = GetNodeID(node);
    NodeDescriptor desc = _NodeDescriptors[nodeID];
    if(_LodLevel > 0 && EvaluateNode(node)){
        _NodeListB.Append(node * 2);
        _NodeListB.Append(node * 2 + uint2(1,0));
        _NodeListB.Append(node * 2 + uint2(0,1));
        _NodeListB.Append(node * 2 + uint2(1,1));
        desc.branch = 1;
    }else{
        _FinalNodeList.Append(uint3(node, _LodLevel));
        desc.branch = 0;
    }
    _NodeDescriptors[nodeID] = desc;
}

float2 GetNodePositionWS2(uint2 node,uint mip){
    float nodeMeterSize = _NodeParams[mip].x;
    float nodeCount = _NodeParams[mip].z;
    float2 nodePositionWS = ((float2)node - (nodeCount-1)*0.5) * nodeMeterSize;
    return nodePositionWS;
}

RenderPatch CreatePatch(uint3 node, uint2 patchOffset){
    uint lod = node.z;
    float nodeMeterSize = _NodeParams[lod].x;
    float patchMeterSize = nodeMeterSize / PATCH_COUNT_PER_NODE;
    float2 nodePositionWS = GetNodePositionWS2(node.xy, lod);
    uint2 patchLoc = node.xy * PATCH_COUNT_PER_NODE + patchOffset;
    float2 minMaxHeight = _MinMaxHeightTexture.mips[lod][patchLoc].rg * _WorldSize.y 
        + float2(-_BoundsHeightRedundance, _BoundsHeightRedundance);

    RenderPatch patch;
    patch.lod = lod;
    patch.position = nodePositionWS + (patchOffset - (PATCH_COUNT_PER_NODE - 1) * 0.5) * patchMeterSize;
    patch.minMaxHeight = minMaxHeight;
    return patch;
}

Bound GetBound(RenderPatch patch){
    float halfSize = _NodeParams[patch.lod].y;
    Bound bound;
    float3 min, max;
    min.xz = patch.position - halfSize;
    max.xz = patch.position + halfSize;
    min.y = patch.minMaxHeight.x;
    max.y = patch.minMaxHeight.y;

    bound.min = min;
    bound.max = max;
    return bound;
}

bool IsInClipSpace(float4 positionCS)
{
    return positionCS.x > -positionCS.w && positionCS.x < positionCS.w &&
        positionCS.y > -positionCS.w && positionCS.y < positionCS.w &&
        positionCS.z > 0 && positionCS.z < positionCS.w;
}

bool FrustumCull(Bound bound){
    float4 boundVerts[8];
    boundVerts[0] = float4(bound.min, 1);
    boundVerts[1] = float4(bound.max, 1);
    boundVerts[2] = float4(bound.max.x, bound.max.y, bound.min.z, 1);
    boundVerts[3] = float4(bound.max.x, bound.min.y, bound.max.z, 1);
    boundVerts[4] = float4(bound.max.x, bound.min.y, bound.min.z, 1);
    boundVerts[5] = float4(bound.min.x, bound.max.y, bound.max.z, 1);
    boundVerts[6] = float4(bound.min.x, bound.max.y, bound.min.z, 1);
    boundVerts[7] = float4(bound.min.x, bound.min.y, bound.max.z, 1);

    float minX = 1, minY = 1, minZ = 1, maxX = -1, maxY = -1, maxZ = -1;
    //视椎剔除
    bool isInClipSpace = false;
    for (int i = 0; i < 8; i++)
    {
        float4 positionCS = mul(_VPMatrix, boundVerts[i]);
        if (!isInClipSpace && IsInClipSpace(positionCS))
            isInClipSpace = true;

        //ndc的AABB
        float3 ndc = positionCS.xyz / positionCS.w;
        if (minX > ndc.x) minX = ndc.x;
        if (minY > ndc.y) minY = ndc.y;
        if (minZ > ndc.z) minZ = ndc.z;
        if (maxX < ndc.x) maxX = ndc.x;
        if (maxY < ndc.y) maxY = ndc.y;
        if (maxZ < ndc.z) maxZ = ndc.z;
    }
    return !isInClipSpace;
}

[numthreads(8,8,1)]
void BuildPatches(uint3 id : SV_DispatchThreadID, uint3 groupId:SV_GroupID, uint3 groupThreadId:SV_GroupThreadID){
    uint3 node = _RenderNodeList[groupId.x];
    uint2 patchOffset = groupThreadId.xy;
    RenderPatch patch = CreatePatch(node, patchOffset);

    Bound bound = GetBound(patch);
    
    if(FrustumCull(bound)){
        return;
    }

    _CulledPatchList.Append(patch);
}
